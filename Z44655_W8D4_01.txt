*&---------------------------------------------------------------------*
*& Report Z44655_W8D4_01
*&---------------------------------------------------------------------*
*&
*&---------------------------------------------------------------------*
REPORT Z44655_W8D4_01.

TABLES: SPFLI.

PARAMETERS: P_CARRID TYPE SPFLI-CARRID.

DATA: GT_SFLIGHT TYPE STANDARD TABLE OF SFLIGHT,
      GS_FLIGHT  TYPE SFLIGHT.

*&SPWIZARD: DECLARATION OF TABLECONTROL 'TC_9000_01' ITSELF
CONTROLS: TC_9000_01 TYPE TABLEVIEW USING SCREEN 9000.

DATA:     OK_CODE LIKE SY-UCOMM.

CLASS LCL_PROG DEFINITION
  INHERITING FROM /ZHD/CL_HREPORT
  CREATE PRIVATE.

  PUBLIC SECTION.
    DATA GS_SPFLI TYPE SPFLI.
    CLASS-METHODS PUSH_VIEW.

    METHODS EXECUTE REDEFINITION.


ENDCLASS.

CLASS LCL_V9000 DEFINITION CREATE PUBLIC
*  INHERITING FROM /ZHD/CL_HSCREEN FINAL.
  INHERITING FROM /ZHD/CL_HSCREEN_WITH_COMPONENT FINAL.

  PUBLIC SECTION.

    METHODS CONSTRUCTOR.

    METHODS PBO REDEFINITION.
    METHODS PAI REDEFINITION.
    METHODS EXIT REDEFINITION.

  PROTECTED SECTION.

    METHODS GET_DATA.
    METHODS ON_COUNTDOWN_FINISHED REDEFINITION.
    METHODS ADD_COMPONENTS REDEFINITION.

ENDCLASS.

CLASS LCL_TC_SFLIGHT DEFINITION CREATE PUBLIC
  INHERITING FROM /ZHD/CL_HSCREEN_TABLE_CONTROL FINAL.

  PUBLIC SECTION.

    METHODS CONSTRUCTOR
      IMPORTING
        HSCREEN TYPE REF TO /ZHD/CL_HSCREEN
      RAISING
        CX_UUID_ERROR
        /ZHD/CX_HSCREEN.

    METHODS PBO_TC_LINE REDEFINITION.
    METHODS PAI_TC_LINE REDEFINITION.
    METHODS IS_LINE_INSERT REDEFINITION.
    METHODS IS_DELETION_CONFIRMED REDEFINITION.
    METHODS POV REDEFINITION.
    METHODS POH REDEFINITION.
    METHODS USER_COMMAND_EXTEND REDEFINITION.

ENDCLASS.

CLASS LCL_PROG IMPLEMENTATION.

  METHOD PUSH_VIEW.

    CHECK NOT IS_SCREEN_EXISTS( PROGRAM = SY-REPID ).


    DATA LO_VIEW TYPE REF TO /ZHD/IF_HSCREEN.
    DATA LV_CLSNAM TYPE SEOCLSNAME.
    CASE SY-DYNNR.
      WHEN '1000'.
        LV_CLSNAM = 'LCL_PROG'.
      WHEN OTHERS.
        LV_CLSNAM = 'LCL_V' && SY-DYNNR.
    ENDCASE.

    CREATE OBJECT LO_VIEW TYPE (LV_CLSNAM).

  ENDMETHOD.

  METHOD EXECUTE.

    CALL SCREEN 9000.

  ENDMETHOD.

ENDCLASS.

CLASS LCL_V9000 IMPLEMENTATION.

  METHOD CONSTRUCTOR.

    SUPER->CONSTRUCTOR( ).
    ADD_COMPONENTS( ).
    GET_DATA( ).

  ENDMETHOD.

  METHOD GET_DATA.

    SELECT SINGLE * FROM SPFLI INTO SPFLI WHERE CARRID = P_CARRID.

  ENDMETHOD.

  METHOD PBO.

    SET PF-STATUS SY-DYNNR.

  ENDMETHOD.

  METHOD EXIT.

    CASE SY-UCOMM.
      WHEN '&ZBACK' OR '&ZEXIT'.
        LEAVE TO SCREEN 0.
      WHEN '&ZEND'.
        LEAVE PROGRAM.
      WHEN OTHERS.
    ENDCASE.

  ENDMETHOD.

  METHOD PAI.

    CASE UCOMM.
      WHEN 'DIS_MODE'.
        IF GET_DISPLAY_MODE( ) = /ZHD/CL_HSCREEN=>DISPLAY_MODE_MODIFY.
          SET_DISPLAY_MODE( /ZHD/CL_HSCREEN=>DISPLAY_MODE_SHOW ).
          COUNTDOWN_BEGIN( 3 ).
        ELSE.
          SET_DISPLAY_MODE( /ZHD/CL_HSCREEN=>DISPLAY_MODE_MODIFY ).
        ENDIF.
      WHEN OTHERS.
    ENDCASE.

    CLEAR SY-UCOMM.

  ENDMETHOD.

  METHOD ADD_COMPONENTS.

    NEW LCL_TC_SFLIGHT( ME ).

  ENDMETHOD.

  METHOD ON_COUNTDOWN_FINISHED.

    GET_DATA( ).
    SCREEN_UTIL->EXECUTE_FCODE( '/00' ).

  ENDMETHOD.

ENDCLASS.

CLASS LCL_TC_SFLIGHT IMPLEMENTATION.

  METHOD CONSTRUCTOR.

    SUPER->CONSTRUCTOR( PARENT             =  HSCREEN
                        TC_NAME            = 'TC_9000_01'
                        DATA_SOURCE        = 'GT_SFLIGHT'
                        DATA_WA            = 'GS_FLIGHT'
                        REF_STRUCTURE_NAME = 'SFLIGHT'
                        SELBAR_NAME        = 'MANDT' ).

    SELECT *
     FROM SFLIGHT
    INTO TABLE GT_SFLIGHT WHERE CARRID = P_CARRID.


  ENDMETHOD.

  METHOD PBO_TC_LINE.

    SUPER->PBO_TC_LINE( ).

    CHECK PARENT->DISPLAY_MODE = /ZHD/CL_HSCREEN=>DISPLAY_MODE_MODIFY.

    IF GS_FLIGHT-CONNID = '17'.
      SET_CELL_DISPLAY( 'GS_FLIGHT-CURRENCY' ).
    ELSE.
      SET_CELL_EDITABLE( 'GS_FLIGHT-CURRENCY' ).
    ENDIF.

  ENDMETHOD.

  METHOD PAI_TC_LINE.

    SUPER->PAI_TC_LINE( ).

  ENDMETHOD.

  METHOD IS_LINE_INSERT.

    IS_LINE_INSERT = SUPER->IS_LINE_INSERT( ).

    DATA(LV_INDEX) = LINES( GT_SFLIGHT ).

    GT_SFLIGHT[ LV_INDEX ]-CARRID = SPFLI-CARRID.

    IF GT_SFLIGHT[ LV_INDEX ]-CONNID IS INITIAL.
      IF LV_INDEX = 1.
        GT_SFLIGHT[ LV_INDEX ]-CONNID = 1.
      ELSE.
        GT_SFLIGHT[ LV_INDEX ]-CONNID = GT_SFLIGHT[ LV_INDEX - 1 ]-CONNID + 1.
      ENDIF.
    ENDIF.

  ENDMETHOD.

  METHOD IS_DELETION_CONFIRMED.

    DATA ANSWER TYPE C.
    CALL FUNCTION 'POPUP_TO_CONFIRM'
      EXPORTING
        TEXT_QUESTION  = 'Delete Row:' && GT_SFLIGHT[ INDEX ]-CONNID
      IMPORTING
        ANSWER         = ANSWER
      EXCEPTIONS
        TEXT_NOT_FOUND = 1
        OTHERS         = 2.
    IF SY-SUBRC = 0 AND ANSWER = '1'.
      IS_CONFIRMED = ABAP_TRUE.
    ENDIF.

  ENDMETHOD.

  METHOD POV.

    CASE PARENT->CURSOR_FILED.

      WHEN 'GS_FLIGHT-CURRENCY'.

        SELECT CURRKEY,
               CURRDEC
          FROM SCURX
         INTO TABLE @DATA(LT_SFLIGHT)
        UP TO '500' ROWS
        ORDER BY CURRKEY.

        GS_FLIGHT-CURRENCY = PARENT->SCREEN_UTIL->F4_EVENT( KEY_FIELD = 'CURRKEY' VALUE_TAB = LT_SFLIGHT ).

    ENDCASE.

*    SUPER->POV( ).

  ENDMETHOD.

  METHOD POH.

    CASE PARENT->CURSOR_FILED.

      WHEN 'GS_FLIGHT-CARRID'.
        MESSAGE '' && GS_FLIGHT-CARRID TYPE 'I'.
      WHEN OTHERS.
    ENDCASE.

*    SUPER->POV( ).

  ENDMETHOD.

  METHOD USER_COMMAND_EXTEND.

    CASE UCOMM.
      WHEN 'TEST1'.
        MESSAGE 'TC_9000_01_TEST1 Success' TYPE 'I'.
      WHEN OTHERS.
    ENDCASE.

  ENDMETHOD.

ENDCLASS.

INCLUDE /ZHD/HSCREEN_EVENT_INC.

**&SPWizard: Data incl. inserted by SP Wizard. DO NOT CHANGE THIS LINE!
*INCLUDE Z44655_W8D4_01_TC .
